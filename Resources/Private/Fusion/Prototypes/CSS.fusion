prototype(GesagtGetan.CookieDialogue:CSS) < prototype(Neos.Fusion:Tag) {
    position = ${Configuration.setting('GesagtGetan.CookieDialogue.css.position')}
    style = ${Configuration.setting('GesagtGetan.CookieDialogue.css.style')}
    container = ${Configuration.setting('GesagtGetan.CookieDialogue.css.container')}
    maxWith = ${Configuration.setting('GesagtGetan.CookieDialogue.css.maxWith')}
    link = ${Configuration.setting('GesagtGetan.CookieDialogue.css.link')}
    button = ${Configuration.setting('GesagtGetan.CookieDialogue.css.button')}

    // Prefix helpers
    flex = 'display:-webkit-box;display:-ms-flexbox;display:flex;'
    flexColumn = '-webkit-box-orient:vertical;-webkit-box-direction:normal;-ms-flex-direction:column;flex-direction:column;'
    flexReverse = '-webkit-box-orient:horizontal;-webkit-box-direction:reverse;-ms-flex-direction:row-reverse;flex-direction:row-reverse;'
    alignCenter = '-webkit-box-align:center;-ms-flex-align:center;align-items:center;'
    justifyBetween = '-webkit-box-pack:justify;-ms-flex-pack:justify;justify-content:space-between;'
    userSelect = '-webkit-user-select:none;-moz-user-select:none;-ms-user-select:none;user-select:none;'

    @context {
        position = ${this.position}
        style = ${this.style}
        container = ${this.container}
        maxWith = ${this.maxWith}
        link = ${this.link}
        button = ${this.button}
        flex = ${this.flex}
        flexColumn = ${this.flexColumn}
        flexReverse = ${this.flexReverse}
        alignCenter = ${this.alignCenter}
        justifyBetween = ${this.justifyBetween}
        userSelect = ${this.userSelect}
    }

    @if.render = ${Configuration.setting('GesagtGetan.CookieDialogue.css.include')}

    tagName = 'style'
    content = Neos.Fusion:Array {
        cookieDialogue = ${flex + alignCenter + userSelect + 'left:0;right:0;line-height:1.4;padding:1em 0 0;position:fixed;transition:transform .5s cubic-bezier(.165,.84,.44,1);z-index:10050;'}
        cookieDialogue.@process {
            position = ${value + position + ':0;transform:translateY(' + (position == 'top' ? '-' : '') + '110%);'}
            style = ${value + (style == 'center' ? flexColumn : justifyBetween)}
            align = ${value + 'text-align:' + style + ';'}
            wrap = ${className + '{' + value + container + '}'}
        }
        div = ${className + ' div{' + flex + flexColumn + '}@media(min-width:600px){' + className + ' div{' + flexReverse + '}}'}
        div.@if.isLeft = ${style == 'left'}
        visible = ${className + '.-visible{transform:translateY(0)}'}
        paragraph = ${className + ' p{margin:0 1em 1em' + (maxWith ? ';max-width:' + maxWith : '') + '}'}
        buttonNormal = ${className + ' button{cursor:pointer;display:inline-block;margin:0 1em 1em;padding:5px 24px;text-decoration:none;border-radius:1px;' + button.normal + '}'}
        buttonHover = ${className + ' button:focus,' + className + ' button:hover{' + button.hover + '}' + className + ' button:focus{outline:none}'}
        linkNormal = ${className + ' a,' + className + ' button[style]{cursor:pointer;' + link.normal + '}'}
        linkHover = ${className + ' a:hover,' + className + ' a:focus,' + className + ' button[style]:hover,' + className + ' button[style]:focus{' + link.hover + '}'}
    }
}
